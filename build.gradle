def vJavaLang = '1.7'

buildscript {

    repositories {
        maven { url "http://repo.spring.io/libs-release" }
        maven { url "http://repo.spring.io/milestone/" }
        maven { url "http://repo.spring.io/libs-snapshot" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.1.RELEASE")
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.3.0'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'
apply plugin: 'coveralls'


targetCompatibility = vJavaLang
sourceCompatibility = vJavaLang


jar {
    baseName = 'hello-travis'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-release" }
    maven { url "http://repo.spring.io/milestone/" }
    maven { url "http://repo.spring.io/libs-snapshot" }

}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        // prefer Jetty
        exclude module: "spring-boot-starter-tomcat"
    }
    // spring-boot-starter-jetty fetches Jetty 8 ... too old!
    compile 'org.eclipse.jetty:jetty-webapp:9.+'
    compile("org.springframework.boot:spring-boot-starter-actuator")
    testCompile("junit:junit")
    testCompile "org.springframework:spring-test"
}

configurations {
    // replaced with jcl-over-slf4j
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    // replaced with log4j-over-slf4j
    all*.exclude group: 'log4j', module: 'log4j'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task stage(dependsOn: ['assemble'])